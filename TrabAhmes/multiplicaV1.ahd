; 1. Inicio: i=8; Pmsb=0, maux=m
       LDA Oito
       STA i
       LDA Zero
       STA Pmsb
       LDA mcador
       STA maux
; 2. Desloca maux p/direita, bit menos significativo vai p/carry
laco:  LDA maux
       SHR
       STA maux
; 3. Se carry = 0, pula a soma (passo 4)
       JNC C0
; 4. P = P + M
       LDA Pmsb
       ADD Mcando
       STA Pmsb
; 5 Desloca P e p, com carry
C0:    LDA Pmsb
       ROR
       STA Pmsb
       LDA plsb
       ROR
       STA plsb
; 6. Decrementa contador (i)
       LDA i
       SUB Um
       STA i
; 7. Se n!=0, voltar para o passo 2
       JNZ laco
; Se não, terminar
       HLT

ORG 128
Mcando: DB  255
Mcador: DB    1
Pmsb:   DB    0
Plsb:   DB    0
i:      DB    0
maux:   DB    0
Zero:   DB    0
Um:     DB    1
Oito:   DB    8
